apply plugin: 'com.android.library'
apply plugin: 'maven'
android {

    compileSdkVersion 27
    buildToolsVersion '28.0.3'

    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = new File(".", output.outputFile.name.replace(".aar", "_v${defaultConfig.versionName}.aar"))
        }
    }

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.5.0"
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    android {
        lintOptions {
            abortOnError false
        }
    }

    resourcePrefix 'rsb_'
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:preference-v14:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.google.code.gson:gson:2.4'
    implementation 'io.reactivex:rxjava:1.1.3'
    implementation 'io.reactivex:rxandroid:1.1.0'
    implementation 'com.jakewharton.rxbinding:rxbinding:0.2.0'
    implementation 'com.jakewharton.rxbinding:rxbinding-support-v4:0.2.0'
    implementation 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.2.0'
    implementation 'com.jakewharton.rxbinding:rxbinding-design:0.2.0'
    implementation 'com.scottyab:aes-crypto:0.0.3'
    implementation 'co.touchlab.squeaky:squeaky-query:0.4.0.0'
    annotationProcessor 'co.touchlab.squeaky:squeaky-processor:0.4.0.0'
    implementation 'net.zetetic:android-database-sqlcipher:3.5.4@aar'
    implementation 'com.afollestad.material-dialogs:core:0.9.0.2'
    implementation "android.arch.lifecycle:extensions:1.1.1"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.0'
    testImplementation 'org.mockito:mockito-core:1.10.19'
}

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        failOnError false
        source = android.sourceSets.main.java.srcDirs
    }

    afterEvaluate {
        javadoc.classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        javadoc.classpath += files(android.libraryVariants.collect { variant ->
            variant.javaCompile.classpath.files
        })
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

def RS_POM_GROUP="com.medable.researchstack"
def RS_POM_DESCRIPTION="Medable's ResearchStack fork"
def RS_POM_LICENCE_NAME="The Apache Software License, Version 2.0"
def RS_POM_LICENCE_URL="http://www.apache.org/licenses/LICENSE-2.0.txt"
def RS_POM_LICENCE_DIST="repo"

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {

                pom.groupId = RS_POM_GROUP
                pom.artifactId = project.getProperties().BB_POM_ARTIFACT_ID
                pom.version = android.defaultConfig.versionName

                def deployPath = file('./')
                repository(url: "file://${deployPath.absolutePath}/../../Android-SDK/")

                pom.project {
                    name project.getProperties().BB_POM_NAME
                    packaging project.getProperties().BB_POM_PACKAGING
                    description RS_POM_DESCRIPTION

                    licenses {
                        license {
                            name RS_POM_LICENCE_NAME
                            url RS_POM_LICENCE_URL
                            distribution RS_POM_LICENCE_DIST
                        }
                    }
                }

                pom.whenConfigured { pom ->
                    pom.dependencies.forEach { dep ->
                        if (dep.getVersion() == "unspecified") {
                            dep.setGroupId(RS_POM_GROUP)
                            dep.setVersion(android.defaultConfig.versionName)
                        }
                    }
                }
            }
        }
    }
}

buildscript {
    repositories {
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
}