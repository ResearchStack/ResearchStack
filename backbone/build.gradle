apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'maven'

android {

    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parentFile,
                    output.outputFile.name.replace(".aar", "_v${VERSION_NAME}.aar"));
        }
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 6
        versionName version
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    resourcePrefix 'rsb_'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:cardview-v7:25.2.0'
    compile 'com.android.support:preference-v14:25.2.0'
    compile 'com.android.support:design:25.2.0'
    compile 'com.google.code.gson:gson:2.4'
    compile 'io.reactivex:rxjava:1.1.3'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'com.jakewharton.rxbinding:rxbinding:0.2.0'
    compile 'com.jakewharton.rxbinding:rxbinding-support-v4:0.2.0'
    compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.2.0'
    compile 'com.jakewharton.rxbinding:rxbinding-design:0.2.0'
    compile 'com.github.PhilJay:MPAndroidChart:v2.2.3'

    compile 'com.scottyab:aes-crypto:0.0.3'
    compile 'co.touchlab.squeaky:squeaky-query:0.4.0.0'
    annotationProcessor 'co.touchlab.squeaky:squeaky-processor:0.4.0.0'
    compile 'net.zetetic:android-database-sqlcipher:3.5.4@aar'
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        failOnError false
        source = android.sourceSets.main.java.srcDirs
    }

    afterEvaluate {
        javadoc.classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        javadoc.classpath += files(android.libraryVariants.collect { variant ->
            variant.javaCompile.classpath.files
        })
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {

                pom.groupId = GROUP
                pom.artifactId = project.getProperties().POM_ARTIFACT_ID
                pom.version = VERSION_NAME

                def deployPath = file('./')
                repository(url: "file://${deployPath.absolutePath}/../../Android-SDK/")

                pom.project {
                    name project.getProperties().POM_NAME
                    packaging project.getProperties().POM_PACKAGING
                    description POM_DESCRIPTION

                    licenses {
                        license {
                            name POM_LICENCE_NAME
                            url POM_LICENCE_URL
                            distribution POM_LICENCE_DIST
                        }
                    }
                }

                pom.whenConfigured { pom ->
                    pom.dependencies.forEach { dep ->
                        if (dep.getVersion() == "unspecified") {
                            dep.setGroupId(GROUP)
                            dep.setVersion(VERSION_NAME)
                        }
                    }
                }

            }
        }
    }
}

