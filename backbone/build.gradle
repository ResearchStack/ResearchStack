apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'
apply plugin: "maven"

version = '2.2.3'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 116
        versionName version
        vectorDrawables.useSupportLibrary = true

        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError false
    }

    resourcePrefix 'rsb_'
}

// Reading in data from local.properties is used here to grab key/value pairs used below in ext
// Originally, it read the local.properties, but this file should never be committed to vcs,
// So check if it exists first, because some projects may not care about it
Properties properties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

ext {
    bintrayRepo = 'ResearchStack'
    bintrayName = 'Backbone'

    publishedGroupId = 'com.medaptivehealth.researchstack'
    libraryName = 'Backbone'
    artifact = 'backbone'

    libraryDescription = 'Backbone description'

    siteUrl = 'https://researchstack.org'
    gitUrl = 'https://github.com/ResearchStack/ResearchStack.git'

    libraryVersion = version

    // This grabs the key/value pairs from local.properties and assigns them to variables that
    // can be used in gradle, and specifically, the mavenInstaller below
    userOrgName = 'medaptivehealth'
    developerId = properties.getProperty("bintray.user")
    developerName = properties.getProperty("bintray.developerName")
    developerEmail = properties.getProperty("bintray.developerEmail")

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // These are all support libraries that should be updated when Google releases new ones
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'io.reactivex:rxjava:1.3.0'
    implementation 'nl.littlerobots.rxlint:rxlint:1.6.1'
    implementation 'com.google.code.gson:gson:2.8.6'

    implementation 'com.cronutils:cron-utils:3.1.2', {
        exclude group: 'joda-time', module: 'joda-time' // this can cause dex errors due to duplicate joda times
    }
    api 'net.danlew:android.joda:2.9.9.4' // android specific joda time, jre version causes perf problems
    testImplementation 'joda-time:joda-time:2.9.9'

    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'

    implementation 'io.reactivex:rxandroid:1.2.1'

    implementation 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    implementation 'com.jakewharton.rxbinding:rxbinding-support-v4:0.4.0'
    implementation 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.4.0'
    implementation 'com.jakewharton.rxbinding:rxbinding-design:0.4.0'

    // Used to display UploadData and study data in various chart formats
    implementation 'com.github.PhilJay:MPAndroidChart:v2.2.3'

    implementation 'com.scottyab:aes-crypto:0.0.3'
    implementation 'co.touchlab.squeaky:squeaky-query:0.4.0.0'
    annotationProcessor 'co.touchlab.squeaky:squeaky-processor:0.4.0.0'
    implementation 'net.zetetic:android-database-sqlcipher:4.0.1@aar'

    // Libraries to help with unit testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:3.2.4'
    testImplementation "org.powermock:powermock-module-junit4:1.7.1"
    testImplementation "org.powermock:powermock-module-junit4-rule:1.7.1"
    testImplementation "org.powermock:powermock-api-mockito2:1.7.4"
    testImplementation "org.powermock:powermock-classloading-xstream:1.7.1"

    testImplementation 'org.robolectric:robolectric:3.5'
}

group = publishedGroupId                               // Maven Group ID for the artifact

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId publishedGroupId
                artifactId artifact

                // Add your description here
                name libraryName
                description libraryDescription
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        failOnError false
        source = android.sourceSets.main.java.srcDirs
        // Exclude generated files
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
    }

    afterEvaluate {
        javadoc.classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        javadoc.classpath += files(android.libraryVariants.collect { variant ->
            variant.javaCompile.classpath.files
        })
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
