apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'maven'
android {

    compileSdkVersion 25
    buildToolsVersion "26.0.3"

    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = new File(".", output.outputFile.name.replace(".aar", "_v${defaultConfig.versionName}.aar"))
        }
    }

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 22
        versionCode 6
        versionName "1.0.7"
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    android {
        lintOptions {
            abortOnError false
        }
    }

    resourcePrefix 'rsb_'
}
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:cardview-v7:25.2.0'
    compile 'com.android.support:preference-v14:25.2.0'
    compile 'com.android.support:design:25.2.0'
    compile 'com.google.code.gson:gson:2.4'
    compile 'io.reactivex:rxjava:1.1.3'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'com.jakewharton.rxbinding:rxbinding:0.2.0'
    compile 'com.jakewharton.rxbinding:rxbinding-support-v4:0.2.0'
    compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.2.0'
    compile 'com.jakewharton.rxbinding:rxbinding-design:0.2.0'
    compile 'com.github.PhilJay:MPAndroidChart:v2.2.3'
    compile 'com.scottyab:aes-crypto:0.0.3'
    compile 'co.touchlab.squeaky:squeaky-query:0.4.0.0'
    annotationProcessor 'co.touchlab.squeaky:squeaky-processor:0.4.0.0'
    compile 'net.zetetic:android-database-sqlcipher:3.5.4@aar'
    compile 'com.afollestad.material-dialogs:core:0.9.0.2'
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        failOnError false
        source = android.sourceSets.main.java.srcDirs
    }

    afterEvaluate {
        javadoc.classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        javadoc.classpath += files(android.libraryVariants.collect { variant ->
            variant.javaCompile.classpath.files
        })
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {

                pom.groupId = RS_POM_GROUP
                pom.artifactId = project.getProperties().BB_POM_ARTIFACT_ID
                pom.version = android.defaultConfig.versionName

                def deployPath = file('./')
                repository(url: "file://${deployPath.absolutePath}/../../Android-SDK/")

                pom.project {
                    name project.getProperties().BB_POM_NAME
                    packaging project.getProperties().BB_POM_PACKAGING
                    description RS_POM_DESCRIPTION

                    licenses {
                        license {
                            name RS_POM_LICENCE_NAME
                            url RS_POM_LICENCE_URL
                            distribution RS_POM_LICENCE_DIST
                        }
                    }
                }

                pom.whenConfigured { pom ->
                    pom.dependencies.forEach { dep ->
                        if (dep.getVersion() == "unspecified") {
                            dep.setGroupId(RS_POM_GROUP)
                            dep.setVersion(android.defaultConfig.versionName)
                        }
                    }
                }
            }
        }
    }
}

buildscript {
    repositories {
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
}